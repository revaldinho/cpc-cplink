10 REM Raytracing program converted for CPC/Pi GPU 
20 REM https://stardot.org.uk/forums/viewtopic.php?f=54&t=15825&p=217419&hilit=ray+trace#p217015
30 MEMORY &97FF:LOAD "FIFORSX.bin",&9800:CALL &9800
40 |VDU,22,2 :|VDU,23,&A,20,0,0,0,0,0,0,0
50 st=TIME
60 DATA 4,1,5,2,6,3
70 FOR n%=1 TO 6:READ c%:|VDU,19,n%,c%,0,0,0:NEXT n%
80 DATA 0,6,1,7,5
90 DIM cl%(5):FOR n%=1 TO 5:READ cl%(n%):NEXT n%
100 s%=4:DIM c(s%,3):DIM r(s%):DIM q(s%)
110 FOR k%=1 TO s%:READ c(k%,1),c(k%,2),c(k%,3),r:r(k%)=r:q(k%)=r*r:NEXT k%
120 DATA -0.8,-1,3.2,0.7
130 DATA 0,-0.45,2,0.3
140 DATA 1.2,-0.7,2.5,0.5
150 DATA 0.4,-1,4,0.4
160 FOR i%=0 TO 1024 STEP 4:FOR j%=0 TO 1280 STEP 8
170 x=0.3:y=-0.5:z=0:ba%=3:dx=j%-640:dy=512-i%:dz=1300:dd=dx*dx+dy*dy+dz*dz
180 n%=(y>=0 OR dy<=0):IF NOT n% THEN ss=-y/dy
190 FOR k%=1 TO s%
200 px=c(k%,1)-x:py=c(k%,2)-y:pz=c(k%,3)-z
210 sc=px*dx+py*dy+pz*dz
220 IF sc<=0 THEN GOTO 270
230 bb=sc*sc/dd
240 aa=q(k%)-(px*px+py*py+pz*pz)+bb
250 IF aa<=0 THEN GOTO 270
260 sc=(SQR(bb)-SQR(aa))/SQR(dd):IF sc<ss OR n%<0 THEN n%=k%:ss=sc
270 NEXT k%
280 IF n%<0 THEN col%=1+(dy*dy/dd)*7:n%=0:GOTO 410
290 dx=dx*ss:dy=dy*ss:dz=dz*ss:dd=dd*ss*ss
300 x=x+dx:y=y+dy:z=z+dz
310 IF n%=0 THEN GOTO 370
320 nx=x-c(n%,1):ny=y-c(n%,2):nz=z-c(n%,3)
330 nn=nx*nx+ny*ny+nz*nz
340 l=2*(dx*nx+dy*ny+dz*nz)/nn
350 dx=dx-nx*l:dy=dy-ny*l:dz=dz-nz*l
360 GOTO 180
370 FOR k%=1 TO s%
380 u=c(k%,1)-x:v=c(k%,3)-z:IF u*u+v*v<=q(k%) THEN ba%=1
390 NEXT k%
400 IF (x-INT(x)>0.5)=(z-INT(z)>0.5) THEN col%=cl%(ba%) ELSE col%=cl%(ba%+1)
410 |VDU,18,0,col%:|VDU,25,69,j%,i%
420 NEXT j%
425 PRINT "Finished Row ",i%, "Time ", (TIME-st)/300;"s"
427 NEXT i%
430 |FIFOPUTS, "Runtime:"+STR$((TIME-st)/300)+" s"
440 END